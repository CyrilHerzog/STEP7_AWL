FUNCTION "OBJ_MUX" : WORD
TITLE =
//Autor: Herzog Cyril, BIS_BA
AUTHOR : HECY
FAMILY : BIS_BA
NAME : OBJ_MUX
VERSION : 0.1


VAR_INPUT
  pList : ANY ;	
  pObject : ANY ;	
  iIndex : INT ;	
END_VAR
VAR_TEMP
  _dwSaveAR2 : DWORD ;	
  _bTemp : BOOL ;	
  _stList : STRUCT 	
   iDB : INT ;	
   iSize : INT ;	
   iNumObj : INT ;	
  END_STRUCT ;	
  _stObj : STRUCT 	
   iDB : INT ;	
   iSize : INT ;	
  END_STRUCT ;	
  _iIndex : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =Adressregister sichern

      SET   ; 
      TAR2  #_dwSaveAR2; 


NETWORK
TITLE =Formatprüfung des Zielobjekts

      L     P##pObject; 
      LAR1  ; 
      L     B [AR1,P#1.0]; // Datentyp
      L     2; 
      ==I   ; 
      =     #_bTemp; // Basistyp ist "Byte"
      L     W [AR1,P#4.0]; // Datenbausteinnummer
      T     #_stObj.iDB; 
      L     W [AR1,P#2.0]; // Wiederholungsfaktor => Länge in Bytes
      T     #_stObj.iSize; 
      L     1; 
      >=I   ; 
      =     #_bTemp; // Zusammengesetzter Datentyp => Array, Struct
      SPB   j020; 
      L     W#16#8000; // Ungültiges Datenformat
      T     #RET_VAL; 
      BEA   ; 
j020: L     D [AR1,P#6.0]; 
      LAR1  ; 

NETWORK
TITLE =Formatprüfung der Liste

      L     P##pList; 
      LAR2  ; 
      L     B [AR2,P#1.0]; // Datentyp
      L     2; 
      ==I   ; 
      =     #_bTemp; // Basistyp ist "Byte"
      L     W [AR2,P#4.0]; // Datenbausteinnummer
      T     #_stList.iDB; 
      L     W [AR2,P#2.0]; // Wiederholungsfaktor => Länge in Bytes
      T     #_stList.iSize; 
      L     #_stObj.iSize; 
      /I    ; // "Grösse Liste" / "Grösse Objekt"
      T     #_stList.iNumObj; // Anzahl verfügbarer Objekte in Liste
      SRD   16; // Modulo erhalten
      L     0; 
      ==I   ; 
      =     #_bTemp; // Grösse der Liste ist ein vielfaches der Objektgrösse
      SPB   j040; 
      L     W#16#8000; // Fehler => Falsches Datenformat
      T     #RET_VAL; 
      LAR2  #_dwSaveAR2; 
      BEA   ; 



NETWORK
TITLE =Selektivzeiger limitieren

j040: L     #iIndex; 
      L     #_stList.iNumObj; 
      MOD   ; 
      T     #_iIndex; 

NETWORK
TITLE =Addresszeiger berechnen

      L     D [AR2,P#6.0]; 
      LAR2  ; // Zeiger auf Datenpunkt setzen  
      L     #_iIndex; // iIndex * Objektgrösse(Anzahl Bytes)
      ITD   ; 
      L     #_stObj.iSize; 
      ITD   ; 
      *D    ; 
      SLD   3; // Ergebnis auf Zeigerformat bringen
      +AR2  ; 
      L     #_stObj.iSize; 


NETWORK
TITLE =Kopierschleife

j060: T     #_stObj.iSize; 
      AUF   DB [#_stList.iDB]; 
      L     B [AR2,P#0.0]; 
      AUF   DB [#_stObj.iDB]; 
      T     B [AR1,P#0.0]; 
      +AR1  P#1.0; 
      +AR2  P#1.0; 
      L     #_stObj.iSize; 
      LOOP  j060; 

NETWORK
TITLE =

      L     0; 
      T     #RET_VAL; 
      LAR2  #_dwSaveAR2; 
      BE    ; 


END_FUNCTION

