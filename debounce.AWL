TYPE "DEBOUNCE_TYPE"
AUTHOR : HECY
FAMILY : BIS_BA
NAME : DEB_TYPE
VERSION : 0.1


  STRUCT 	
   stParam : STRUCT 	
    tTime : TIME ;	//Entprellzeit
   END_STRUCT ;	
   stPrivate : STRUCT 	//Private Variablen (Instanz)
    bInFp : BOOL ;	
    bInFn : BOOL ;	
    bTon : BOOL ;	
    bTof : BOOL ;	
    diTicksPre : DINT ;	
   END_STRUCT ;	
  END_STRUCT ;	
END_TYPE

FUNCTION "DEBOUNCE" : BOOL
TITLE =DEBOUNCE
//Herzog Cyril
//18.09.2023
AUTHOR : HECY
FAMILY : BIS_BA
NAME : DEBOUNCE
VERSION : 0.1


VAR_INPUT
  bIn : BOOL ;	
END_VAR
VAR_IN_OUT
  pDebounce_Type : "DEBOUNCE_TYPE";	
END_VAR
VAR_TEMP
  _dwSaveAR2 : DWORD ;	
  _stData : STRUCT 	
   iDB : INT ;	
   bInFP : BOOL ;	
   bInFN : BOOL ;	
   bTON : BOOL ;	
   bTOF : BOOL ;	
   bIn : BOOL ;	
   bOut : BOOL ;	
   bTemp : BOOL ;	
  END_STRUCT ;	
END_VAR
BEGIN
NETWORK
TITLE =

      SET   ; 
      TAR2  #_dwSaveAR2; 
      LAR2  P##_stData; 

NETWORK
TITLE =

      L     P##pDebounce_Type; 
      LAR1  ; 
      L     W [AR1,P#0.0]; 
      T     #_stData.iDB; 
      L     D [AR1,P#2.0]; 
      LAR1  ; 
      AUF   DB [#_stData.iDB]; 
      L     W [AR1,P#4.0]; // stPrivate[0]
      T     W [AR2,P#2.0]; // _stData[2]


NETWORK
TITLE =

      U     #bIn; 
      =     #_stData.bIn; 
      FP    #_stData.bInFP; 
      =     #_stData.bTemp; 
      U     #_stData.bIn; 
      FN    #_stData.bInFN; 
      O     #_stData.bTemp; 
      SPBN  j001; 
      L     "__SYS_TICK"; 
      T     D [AR1,P#6.0]; // diTicksPre


NETWORK
TITLE =

j001: U     #_stData.bIn; 
      UN    #_stData.bTON; 
      SPBN  j002; 
      L     "__SYS_TICK"; 
      L     D [AR1,P#6.0]; // diTicksPre
      -D    ; 
      L     D [AR1,P#0.0]; // tTime
      >=D   ; 
      =     #_stData.bTON; 
j002: UN    #_stData.bIn; 
      R     #_stData.bTON; 


NETWORK
TITLE =

      UN    #_stData.bIn; 
      UN    #_stData.bTOF; 
      SPBN  j003; 
      L     "__SYS_TICK"; 
      L     D [AR1,P#6.0]; // diTicksPre
      -D    ; 
      L     D [AR1,P#0.0]; // tTime
      >=D   ; 
// bTof
      =     #_stData.bTOF; 
j003: U     #_stData.bIn; 
      R     #_stData.bTOF; 


NETWORK
TITLE =

      U     #_stData.bTON; 
      S     #_stData.bOut; 
      U     #_stData.bTOF; 
      R     #_stData.bOut; 
      U     #_stData.bOut; 
      =     #RET_VAL; 


NETWORK
TITLE =

      L     W [AR2,P#2.0]; // _stData[2]
      T     W [AR1,P#4.0]; // stPrivate[0]
      LAR2  #_dwSaveAR2; 
      BE    ; 


END_FUNCTION

